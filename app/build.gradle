apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias 'mykey'
            keyPassword 'android'
            storeFile file(System.properties['user.home'] + '/.android/playstore.keystore')
            storePassword 'android'
        }
        dev {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file(System.properties['user.home'] + '/.android/debug.keystore')
            storePassword 'android'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileSdkVersion rootProject.ext.get("currentCompileSdkVersion")
    buildToolsVersion = '28.0.3'

    defaultConfig {
        applicationId "com.ndipatri.solarmonitor"

        minSdkVersion rootProject.ext.get("currentMinSdkVersion")
        targetSdkVersion rootProject.ext.get("currentTargetSdkVersion")
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "com.ndipatri.solarmonitor.utils.SolarMonitorEspressoTestRunner"

        multiDexEnabled true

        testProguardFile "proguard-test-rules.txt"

        // pull in properties from 'gradle.properties' in 'app' directory
        resValue("string", "solarEdgeApiKey", solarEdgeApiKey)
        resValue("string", "beaconNamespaceId", beaconNamespaceId)
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.dev

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

final ESPRESSO_VERSION = '3.0.1'
final TEST_RUNNER = '1.0.1'
final DAGGER_VERSION = '2.12'
final RETROFIT_VERSION = '2.6.2'
final SUPPORT_VERSION = rootProject.ext.get("currentSupportLibVersion")
final ARCH_LIFECYCLE_VERSION = rootProject.ext.get("currentArchLifecycleVersion")

dependencies {
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.0'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test:rules:1.1.0'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestImplementation 'androidx.annotation:annotation:1.0.0'
    androidTestImplementation 'org.mockito:mockito-android:2.27.0'
    kaptAndroidTest "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    testImplementation 'org.mockito:mockito-core:2.11.0'

    testImplementation 'junit:junit:4.12'
    testImplementation ('androidx.arch.core:core-testing:2.0.0') {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }
    androidTestImplementation ('androidx.arch.core:core-testing:2.0.0') {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0'

    // Needed '2.1.0-rc01' of Room to support suspending (coroutine) DAO methods.
    implementation 'androidx.room:room-runtime:2.1.0-rc01'
    kapt 'androidx.room:room-compiler:2.1.0-rc01'
    implementation "androidx.room:room-ktx:2.1.0-rc01"

    implementation 'io.reactivex.rxjava2:rxjava:2.1.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation ("com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1") {
        exclude module: 'support-annotations'
    }
    implementation 'com.cantrowitz:rxbroadcast:2.0.0'

    // Both are necessary to do coroutines on Android.
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.1'

    // This lets our Retrofit calls return Deferred instead of RX.  So we can treat
    // them as if they were called using async()
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    // This lets us convert RxJava calls to return Deferred.  We can then all
    // await() on these (as if they were async() calls).
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.2.1'

    // Among other things, this gives us viewModelScope static property on ViewModel
    // which uses default MAIN dispatcher and unified Job scope that is automatically
    // cancelled when the ViewModel is cancelled.
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha01"

    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"

    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.1'

    // need this for synchronizing coroutines making retrofit calls..  since above
    // rxIdler won't work
    implementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'

    // apt command comes from the android-apt plugin
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'commons-codec:commons-codec:1.10'

    implementation 'com.google.code.gson:gson:2.8.1'

    implementation 'com.ndipatri.iot:lib-google-proximity:1.34'

    // NJD TODO - test for bringing in GoogleProximity, should not keep
    // this here!
    // Both of these are used by the underlying GoogleProximity library
    // only so should not need to be pulled in here.. Tried Proguarding
    // these within GoogleProximity library, but didnt' work :-(
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation 'commons-io:commons-io:2.6'

    //androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.25.0'

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"
    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.3.0-M2'

    implementation group: 'com.google.guava', name: 'guava', version: '19.0'
    implementation 'org.altbeacon:android-beacon-library:2.9.2'

    implementation 'joda-time:joda-time:2.9.4'

}