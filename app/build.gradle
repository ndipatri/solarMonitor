apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            keyAlias 'mykey'
            keyPassword 'android'
            storeFile file(System.properties['user.home'] + '/.android/playstore.keystore')
            storePassword 'android'
        }
        dev {
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file(System.properties['user.home'] + '/.android/debug.keystore')
            storePassword 'android'
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    compileSdkVersion rootProject.ext.get("currentCompileSdkVersion")

    defaultConfig {
        applicationId "com.ndipatri.solarmonitor"

        minSdkVersion rootProject.ext.get("currentMinSdkVersion")
        targetSdkVersion rootProject.ext.get("currentTargetSdkVersion")
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        testProguardFile "proguard-test-rules.txt"

        // pull in properties from 'gradle.properties' in 'app' directory
        resValue("string", "solarEdgeApiKey", solarEdgeApiKey)
        resValue("string", "beaconNamespaceId", beaconNamespaceId)
    }

    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.dev

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    buildToolsVersion '27.0.3'
}

final ESPRESSO_VERSION = '3.0.1'
final TEST_RUNNER = '1.0.1'
final DAGGER_VERSION = '2.12'
final RETROFIT_VERSION = '2.2.0'
final SUPPORT_VERSION = rootProject.ext.get("currentSupportLibVersion")
final ARCH_LIFECYCLE_VERSION = rootProject.ext.get("currentArchLifecycleVersion")

dependencies {
    implementation "com.android.support.test.espresso:espresso-idling-resource:${ESPRESSO_VERSION}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${ESPRESSO_VERSION}"
    androidTestImplementation "com.android.support.test.espresso:espresso-web:${ESPRESSO_VERSION}"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:${ESPRESSO_VERSION}"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:${ESPRESSO_VERSION}"
    androidTestImplementation "com.android.support.test:runner:${TEST_RUNNER}"
    androidTestImplementation "com.android.support.test:rules:${TEST_RUNNER}"
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestImplementation "com.android.support:support-annotations:${SUPPORT_VERSION}"
    androidTestImplementation 'org.mockito:mockito-android:2.13.0'
    kaptAndroidTest "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"

    testImplementation 'org.mockito:mockito-core:2.11.0'

    testImplementation 'junit:junit:4.12'
    testImplementation ("android.arch.core:core-testing:${ARCH_LIFECYCLE_VERSION}") {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }
    androidTestImplementation ("android.arch.core:core-testing:${ARCH_LIFECYCLE_VERSION}") {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    }

    implementation "com.android.support:support-v4:${SUPPORT_VERSION}"
    implementation "com.android.support:appcompat-v7:${SUPPORT_VERSION}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation "com.android.support:design:${SUPPORT_VERSION}"

    implementation "android.arch.lifecycle:extensions:${ARCH_LIFECYCLE_VERSION}"
    implementation "android.arch.persistence.room:runtime:${ARCH_LIFECYCLE_VERSION}"
    kapt "android.arch.lifecycle:compiler:${ARCH_LIFECYCLE_VERSION}"
    kapt "android.arch.persistence.room:compiler:${ARCH_LIFECYCLE_VERSION}"

    implementation 'io.reactivex.rxjava2:rxjava:2.0.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation ("com.f2prateek.rx.preferences2:rx-preferences:2.0.0-RC1") {
        exclude module: 'support-annotations'
    }
    implementation 'com.cantrowitz:rxbroadcast:2.0.0'

    implementation "com.squareup.retrofit2:retrofit:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:converter-gson:${RETROFIT_VERSION}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${RETROFIT_VERSION}"

    // apt command comes from the android-apt plugin
    implementation "com.google.dagger:dagger:${DAGGER_VERSION}"
    kapt "com.google.dagger:dagger-compiler:${DAGGER_VERSION}"
    compileOnly 'javax.annotation:jsr250-api:1.0'

    implementation 'commons-codec:commons-codec:1.10'

    implementation 'com.google.code.gson:gson:2.8.1'

    implementation ("com.ndipatri.iot:lib-google-proximity:1.26") {
        exclude module: 'mediarouter-v7'
    }

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$KOTLIN_VERSION"
}